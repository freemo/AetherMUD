# this script is for the shopkeeper chosen to be robbed
ONCE_PROG
QUESTCHAT Help! I've been ROBBED!!
mpsetvar $i ROBBED  ''
mpset $i MONEY 0
~

QUEST_TIME_PROG robbed -1
	mpsetvar $i ROBBED ''
	mpsetvar * ROBBED ''
	mpsetvar $i MONEY ''
	mpsetvar * MONEY ''
~

SPEECH_PROG trouble problem robbed stole rob thief quest help trouble? problem? robbed? stole? rob? thief? quest? help?
	IF VAR($i ROBBED == RECOVER)
		say "$n" I've already been helped, thanks.
	ELSE
		say "$n" That dirty thief made out of here with my gold.  Get my money back and I'll have a reward.
	ENDIF
~

QUEST_TIME_PROG robbed 5
	QUESTCHAT Five minutes to retreive my money, or I'll have to get the law...
~

GIVE_PROG gold
	IF VAR($i ROBBED == RECOVER)
		say "$n" I've already been helped, but thanks.
	ELSE
		IF QUESTWINNER($n robbed)
			say "$n" You've already completed this quest!
			drop $o
		ELSE
			IF VAR($n ROBBED == KILLER)
				IF GOLDAMT($o >= 5000)
					mpsetvar $i ROBBED RECOVER
					mpsetvar $n ROBBED  ''
					say "$n" Thank you very much.  I hope he didn't give you too much trouble.  Here is your reward:
					mpexp $n 100
					mpoload QuestPoint
					give "a quest point" "$n"
					mpoload 100
					give 100 "$n"
					mpquestwin $n robbed
					mpendquest robbed
				ELSE
					say "$n" Are you trying to insult me?  I just got robbed of more than THAT!
					drop $o
				ENDIF
			ELSE
				say "$n" What's this?  This is not my money! Get MY money back!
				drop $o
			ENDIF
		ENDIF
	ENDIF
~