#!QUESTMAKER_START_SCRIPT Delivery #1
#The player delivers a custom-Item from one existing mob in your world
#to another.  A reward is given upon delivery.
#
#!QUESTMAKER_PAGE Quest Name/Duration
#Enter the unique name and starting time/duration for your new quest.
#
#Quest ID:
#Enter a unique name/id for your quest:
#$QUEST_ID=$UNIQUE_QUEST_NAME=delivery_1
#
#Quest Name:
#Enter a friendly displayable name for your quest:
#$QUEST_NAME=$STRING=My New Quest
#
#Quest Frequency:
#A quest starts and stops on timed intervals.  Enter the amount of time 
#that this quest will remain unavailable between runs. This number is in 
#"ticks" (4 second periods).  You may enter math expressions, and use the 
# special ? operator to generate random numbers.  A frequency of
#5 + 3?10 would generate a frequency of 8-15 ticks.
#$FREQUENCY=$EXPRESSION=1350
#
#Quest Duration:
#Enter the amount of time that this quest will run and remain available 
#after it is started.  This time is also the amount of time a player has 
#to delivery the item after they have retreived it from the deliverer mob.
#This time is expressed in "ticks" (4 second periods).  You may enter math 
#expressions, and use the special ? operator to generate random numbers.
#For instance, 5 + 3?10 would generate a duration of 8-15 ticks.
#A duration of 0 will cause the quest to run until your server is shut down.
#$DURATION=$EXPRESSION=1350
#
#Quest-Player Criterium:
#You may leave this entry blank, or enter a Zapper Mask to describe what
#kinds of players may participate in this quest.  You can use this to set
#race, level, or other requirements.
#$QUEST_CRITERIA=$ZAPPERMASK
#
#!QUESTMAKER_PAGE Specify the Deliverer
#The Deliverer is the mob who will be holding the item initially. This must
#be an existing mob in your world.  
#
#Deliverer Area/Room: 
#Specify below the name of the area that your deliverer mob will be located
#in and/or the room in which the mob can be found.  You may leave both blank
#to force the quest manager to find the mob somewhere in the world, but this
#will hurt the performance of your mud, so use with care.
#Use double-quotes around multi-word areas and room ids/names.
#  
#Deliverer Area:
#$DELIVERER_AREA=($AREA)
#Deliverer Room ID:
#$DELIVERER_ROOM=($ROOMID)
#
#Deliverer mob name: 
#Specify the name of the mob who will act as the deliverer mob.  
#Use double-quotes around multi-word names.
#If you specified an area or room above, then the mob must be located there.
#You may optionally have the quest manager select a 'random' mob by entering
#a mob name of ANY MASK= followed by a Zapper Mask (see help on 
#ZAPPERMASKS for information on zapper mask syntax).
#Remember, if the quest manager can not find your mob, the quest will not
#be started.
#$DELIVERER_NAME=$NAME
#
#Deliverer reasons: 
#Specify some additional bit of speech the deliverer 
#mob will say to players when the item is handed over for delivery.  The
#deliverer will already be specifying the name of the mob to deliver to,
#but you might want to say what its for, or where the mob is, or something
#about the reward, or whatever.
#$REASONING=($LONG_STRING)
#
#Deliverer announcement:
#Select what type of annoucement the Deliverer will make when the quest starts.
#$QUEST_ANNOUNCEMENT_TYPE=$CHOOSE=YELL,QUESTCHAT,MPECHO,SAY
#This is what the announcement says.  Leave it blank if you only want him to talk to people who enter the room with him.
#$QUEST_ANNOUNCEMENT=($STRING)
#
#!QUESTMAKER_PAGE Specify the Deliveree
#The Deliveree is the mob to which the item will be delivered. 
#
#Deliveree Area/Room: 
#Specify below the name of the area that your deliveree mob will be located
#in and/or the room in which the mob can be found.  You may leave both blank
#to force the quest manager to find the mob somewhere in the world, but this
#will hurt the performance of your mud, so use with care.  
#Use double-quotes around multi-word areas and room ids/names.
#
#Deliveree Area:
#$DELIVEREE_AREA=($AREA)
#Deliveree Room:
#$DELIVEREE_ROOM=($ROOMID)
#
#Deliveree mob name: 
#Specify the name of the mob who will act as the deliveree mob.  
#Use double-quotes around multi-word names.
#If you specified an area or room above, then the mob must be located there. 
#You may optionally have the quest manager select a 'random' mob by entering
#a mob name of ANY MASK= followed by a Zapper Mask (see help on 
#Prop_HaveZapper for information on zapper mask syntax).
#Remember, if the quest manager can not find your mob, the quest will not
#be started.
#$DELIVEREE_NAME=$NAME
#
#Deliveree response: 
#You may optionally specify some additional bit of speech the deliveree 
#mob will say to the players after the item is handed over. 
#$DELIVER_RESPONSE=($LONG_STRING)=Thank you.
#
#!QUESTMAKER_PAGE Deliverable item
#Create the item which the deliverer mob will give to the player to 
#be taken and given to the deliveree mob.  
#
#Deliverable Item: 
#You may select from the available items below or create a 
#new item just for this quest.
#$DELIVERABLE_ITEM=$ITEMXML
#
#!QUESTMAKER_PAGE Quest Completion
#Select some rewards for delivering the item:
#
#Quest Point?
#$QUEST_POINT=$CHOOSE=YES,NO
#Amount of money (an amount, or leave blank):
#$GOLD=($EXPRESSION)
#Amount of experience points, blank for none, or a number% for percent of exp to next level:
#$EXP=($STRING)=10%
#
#Player Faction to give to or take from:
#$FACTION=($FACTION)
#If you selected a faction above, enter a new numeric amount, 
#or enter +value to add, or --value to subtract:
#$NUMFACTION=($STRING)
#
#Select whether a player may complete this quest multiple times:
#$MULTIPLE=$CHOOSE=YES,NO
#
#If you choose, you may have completion of this quest automatically start another.
#If this is what you want, enter the next quests name here:
#$NEXT_QUEST=($EXISTING_QUEST_NAME)
#
#!QUESTMAKER_END_SCRIPT Delivery #1

set name $QUEST_ID
set display $QUEST_NAME

set wait $FREQUENCY
set interval 1
#quiet

set minplayers 1
set playermask $QUEST_CRITERIA

import items $QUEST_ID_item.xml
set area $DELIVERER_AREA
set room $DELIVERER_ROOM
set mobgroup
set mob $DELIVERER_NAME
give behavior Scriptable LOAD=$QUEST_ID_deliverer.script
load item any
give item
set item ITEM
give behavior Scriptable LOAD=$QUEST_ID_deliverable.script

set area $DELIVEREE_AREA
set room $DELIVEREE_ROOM
set mobgroup
set mob $DELIVEREE_NAME
give behavior Scriptable LOAD=$QUEST_ID_deliveree.script

set duration $DURATION

<?xml version="1.0"?>
<FILE><NAME>$QUEST_ID_item.xml</NAME><DATA><ITEMS>$DELIVERABLE_ITEM</ITEMS></DATA></FILE>
<FILE><NAME>$QUEST_ID_deliverer.script</NAME>
<DATA>
ONCE_PROG 100
	$QUEST_ANNOUNCEMENT_TYPE $QUEST_ANNOUNCEMENT
~

# this script is for the human chosen to be the note bearer
GREET_PROG 100
	if ISLIKE($n '$QUEST_CRITERIA') and ISPC($n)
		if EVAL('$MULTIPLE' == 'YES') OR !QUESTWINNER($n *)
			if has($n '$[1 *]')
					sayto "$n" What are you doing back here?  Deliver $[1 *] to ${2 *}!
			else
				if has($i '$[1 *]')
					sayto "$n" I have something I need to be delivered to ${2 *}.  If you'll deliver it for me, say 'I accept', and I will give you the details.
				endif
			endif
		endif
	endif
~

SPEECH_PROG mission problem package quest help delivery deliver 'me to do' 'to do' mission? problem? package? quest? help? delivery? delive?r 'me to do?' 'to do?' 
	if ISLIKE($n '$QUEST_CRITERIA') and ISPC($n)
		if EVAL('$MULTIPLE' == 'YES') OR !QUESTWINNER($n *)
			if has($n '$[1 *]')
					sayto "$n" Just deliver $[1 *] to ${2 *}!
			else
				if has($i '$[1 *]')
					sayto "$n" I have something I need to be delivered to ${2 *}.  If you'll deliver it for me, say 'I accept', and I will give you the details.
				endif
			endif
		endif
	endif
~

SPEECH_PROG p i accept
	if ISLIKE($n '$QUEST_CRITERIA') and ISPC($n)
		if  EVAL('$MULTIPLE' == 'YES') OR !QUESTWINNER($n *)
			if has($i '$[1 *]') and !has($n '$[1 *]')
	#			mpoload '$[1 *]'
				give "$[1 *]" "$n"
				sayto "$n" Please deliver this to ${2 *}.  $REASONING
				mpqset * REMAINING $DURATION
			else
				if has($n '$[1 *]')
					sayto "$n" Yes, I know.  Now deliver it to ${2 *}.
				endif
			endif
		endif
	endif
~
	

GIVE_PROG all
	if EVAL($o == '$[1 *]')
		sayto "$n" So, you refuse?  Help me find someone else then.  Ok?
		mpjunk $o
	endif
~
</DATA></FILE>
<FILE><NAME>$QUEST_ID_deliverable.script</NAME>
<DATA>
QUEST_TIME_PROG * 1
	mpechoat $w You have less than a minute left to deliver $i.
~
</DATA></FILE>
<FILE><NAME>$QUEST_ID_deliveree.script</NAME>
<DATA>
GREET_PROG 100
	if has($n '$[1 *]')
		say "$n" Ah, I see you are here to deliver $[1 *].  Go ahead and give it to me.
	endif
~
	

GIVE_PROG all
	if ISLIKE($n '$QUEST_CRITERIA') and ISPC($n)
		if  EVAL('$MULTIPLE' != 'YES') AND QUESTWINNER($n *)
			sayto "$n" What's this?
			give "$o" "$n"
		else
			if EVAL($o == '$[1 *]')
				if EVAL('$EXP' != '')
					mpexp $n $EXP
				endif
				if EVAL('$FACTION' != '')
					mpfaction $n $FACTION $NUMFACTION
				endif
				if EVAL('$QUEST_POINT' == 'YES')
					mpoload QuestPoint
					give "a quest point" "$n"
				endif
				if EVAL('$GOLD' != '') AND EVAL('$GOLD' > 0)
					mpoload $GOLD
					give $GOLD "$n"
				endif
				mpquestwin $n *
				mpjunk $o
				sayto "$n" $DELIVER_RESPONSE
				mpendquest *
				IF EVAL('$NEXT_QUEST' != '')
					mpstartquest $NEXT_QUEST
				ENDIF
			else
				sayto "$n" What's this?
				give "$o" "$n"
			endif
		endif
	else
		sayto "$n" What's this?
		give "$o" "$n"
	endif
~
</DATA></FILE>
