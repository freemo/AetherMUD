#!QUESTMAKER_START_SCRIPT Delivery #2
#The player delivers a custom-Item from one existing mob in your world
#to an existing container in your world, such as a pit or mailbox, or
#a room where the item needs to be dropped.
#
#!QUESTMAKER_PAGE Quest Name/Duration
#Enter the unique name and starting time/duration for your new quest.
#
#Quest ID:
#Enter a unique name/id for your quest:
#$QUEST_ID=$UNIQUE_QUEST_NAME=delivery_2
#
#Quest Name:
#Enter a friendly displayable name for your quest:
#$QUEST_NAME=$STRING=My New Quest
#
#Quest Frequency:
#A quest starts and stops on timed intervals.  Enter the amount of time 
#that this quest will remain unavailable between runs. This number is in 
#"ticks" (4 second periods).  You may enter math expressions, and use the 
# special ? operator to generate random numbers.  A frequency of
#5 + 3?10 would generate a frequency of 8-15 ticks.
#$FREQUENCY=$EXPRESSION=1350
#
#Quest Duration:
#Enter the amount of time that this quest will run and remain available 
#after it is started.  This time is also the amount of time a player has 
#to delivery the item after they have retreived it from the deliverer mob.
#This time is expressed in "ticks" (4 second periods).  You may enter math 
#expressions, and use the special ? operator to generate random numbers.
#For instance, 5 + 3?10 would generate a duration of 8-15 ticks.
#A duration of 0 will cause the quest to run until your server is shut down.
#$DURATION=$EXPRESSION=1350
#
#Quest-Player Criterium:
#You may leave this entry blank, or enter a Zapper Mask to describe what
#kinds of players may participate in this quest.  You can use this to set
#race, level, or other requirements.
#$QUEST_CRITERIA=$ZAPPERMASK
#
#!QUESTMAKER_PAGE Specify the Deliverer
#The Deliverer is the mob who will be holding the item initially. This must
#be an existing mob in your world.  
#
#Deliverer Area/Room: 
#Specify below the name of the area that your deliverer mob will be located
#in and/or the room in which the mob can be found.  You may leave both blank
#to force the quest manager to find the mob somewhere in the world, but this
#will hurt the performance of your mud, so use with care.
#Use double-quotes around multi-word areas and room ids/names.
#  
#Deliverer Area:
#$DELIVERER_AREA=($AREA)
#Deliverer Room ID:
#$DELIVERER_ROOM=($ROOMID)
#
#Deliverer mob name: 
#Specify the name of the mob who will act as the deliverer mob.  
#Use double-quotes around multi-word names.
#If you specified an area or room above, then the mob must be located there.
#You may optionally have the quest manager select a 'random' mob by entering
#a mob name of ANY MASK= followed by a Zapper Mask (see help on 
#ZAPPERMASKS for information on zapper mask syntax).
#Remember, if the quest manager can not find your mob, the quest will not
#be started.
#$DELIVERER_NAME=$NAME
#
#Deliverer reasons: 
#Specify some additional bit of speech the deliverer 
#mob will say to players when the item is handed over for delivery.  The
#deliverer will already be specifying the name of the mob to deliver to,
#but you might want to say what its for, or where the mob is, or something
#about the reward, or whatever.
#$REASONING=($LONG_STRING)
#
#Deliverer announcement:
#Select what type of annoucement the Deliverer will make when the quest starts.
#$QUEST_ANNOUNCEMENT_TYPE=$CHOOSE=YELL,QUESTCHAT,MPECHO,SAY
#This is what the announcement says.  Leave it blank if you only want him to talk to people who enter the room with him.
#$QUEST_ANNOUNCEMENT=($STRING)
#
#!QUESTMAKER_PAGE Specify the Destination
#The destination is either a room or a container item.
#
#Destination Area/Room: 
#Specify below the name of the area and room to take the item to.
#You may leave area blank to force the quest manager to find them somewhere in 
#the world, but this will hurt the performance of your mud, so use with care.  
#Use double-quotes around multi-word area/rooms.
#
#Destination Area:
#$DESTINATION_AREA=($AREA)
#Destination Room:
#$DESTINATION_ROOM=$ROOMID
#
#Destination Container:
#Specify below the name of the container item from the above room where
#the item must be deposited to complete the quest.  If you just want 
#the player to drop the item in the room, leave this field blank.
#$DESTINATION_CONTAINER=($NAME)
#
#Destination Description:
#Specify below the name of the destination as it will be described by
#the mob handing over the item.  This should be a short descriptive name
#like "the pit at the temple" or "the mayors headstone at the graveyard"
#$DESTINATION_DESC=$STRING
#
#Destination response: 
#You may optionally specify some additional bit of text said to
#the players after the item is placed. 
#$DELIVER_RESPONSE=($LONG_STRING)=Your task is complete.
#
#!QUESTMAKER_PAGE Deliverable item
#Create the item which the deliverer mob will give to the player to 
#be taken and given to the destination.
#
#Deliverable Item: 
#You may select from the available items below or create a 
#new item just for this quest.
#$DELIVERABLE_ITEM=$ITEMXML
#
#!QUESTMAKER_PAGE Quest Completion
#Select some rewards for delivering the item:
#
#Quest Point?
#$QUEST_POINT=$CHOOSE=YES,NO
#Amount of money (an amount, or leave blank):
#$GOLD=($EXPRESSION)
#Amount of experience points, blank for none, or a number% for percent of exp to next level:
#$EXP=($STRING)=10%
#
#Player Faction to give to or take from:
#$FACTION=($FACTION)
#If you selected a faction above, enter a new numeric amount, 
#or enter +value to add, or --value to subtract:
#$NUMFACTION=($STRING)
#
#Select whether a player may complete this quest multiple times:
#$MULTIPLE=$CHOOSE=YES,NO
#
#If you choose, you may have completion of this quest automatically start another.
#If this is what you want, enter the next quests name here:
#$NEXT_QUEST=($EXISTING_QUEST_NAME)
#
#!QUESTMAKER_END_SCRIPT Delivery #2

set name $QUEST_ID
set display $QUEST_NAME

set wait $FREQUENCY
set interval 1
#quiet

set minplayers 1
set playermask $QUEST_CRITERIA

import items $QUEST_ID_item.xml
set area $DELIVERER_AREA
set room $DELIVERER_ROOM
set mobgroup
set mob $DELIVERER_NAME
give behavior Scriptable LOAD=$QUEST_ID_deliverer.script
load item any
give item
set item ITEM
give behavior Scriptable LOAD=$QUEST_ID_deliverable.script

set area $DESTINATION_AREA
set room $DESTINATION_ROOM
give behavior Scriptable LOAD=$QUEST_ID_deliveree.script
set item $DESTINATION_CONTAINER
give affect Property

set duration $DURATION

<?xml version="1.0"?>
<FILE><NAME>$QUEST_ID_item.xml</NAME><DATA><ITEMS>$DELIVERABLE_ITEM</ITEMS></DATA></FILE>
<FILE><NAME>$QUEST_ID_deliverable.script</NAME>
<DATA>
QUEST_TIME_PROG * 1
	mpechoat $w You have less than a minute left to deliver $i.
~
</DATA></FILE>
<FILE><NAME>$QUEST_ID_deliverer.script</NAME>
<DATA>
ONCE_PROG 100
	$QUEST_ANNOUNCEMENT_TYPE $QUEST_ANNOUNCEMENT
~

# this script is for the human chosen to be the note bearer
GREET_PROG 100
	if ISLIKE($n '$QUEST_CRITERIA') and ISPC($n)
		if EVAL('$MULTIPLE' == 'YES') OR !QUESTWINNER($n *)
			if has($n '$[1 *]')
					sayto "$n" What are you doing back here?  Take $[1 *] to $DESTINATION_DESC!
			else
				if has($i '$[1 *]')
					sayto "$n" I have something I need to be taken to $DESTINATION_DESC.  If you'll deliver it for me, say 'I accept', and I will give you the details.
				endif
			endif
		endif
	endif
~

SPEECH_PROG mission problem package quest help delivery deliver 'me to do' 'to do' mission? problem? package? quest? help? delivery? delive?r 'me to do?' 'to do?' 
	if ISLIKE($n '$QUEST_CRITERIA') and ISPC($n)
		if EVAL('$MULTIPLE' == 'YES') OR !QUESTWINNER($n *)
			if has($n '$[1 *]')
					sayto "$n" Just take $[1 *] to $DESTINATION_DESC!
			else
				if has($i '$[1 *]')
					sayto "$n" I have something I need to be taken to $DESTINATION_DESC.  If you'll deliver it for me, say 'I accept', and I will give you the details.
				endif
			endif
		endif
	endif
~

SPEECH_PROG p i accept
	if ISLIKE($n '$QUEST_CRITERIA') and ISPC($n)
		if  EVAL('$MULTIPLE' == 'YES') OR !QUESTWINNER($n *)
			if has($i '$[1 *]') and !has($n '$[1 *]')
#				mpoload '$[1 *]'
				give "$[1 *]" "$n"
				sayto "$n" Please deliver this to $DESTINATION_DESC.  $REASONING
				mpqset * REMAINING $DURATION
			else
				if has($n '$[1 *]')
					sayto "$n" Yes, I know.  Now deliver it to $DESTINATION_DESC.
				endif
			endif
		endif
	endif
~
	

GIVE_PROG all
	if EVAL($o == '$[1 *]')
		sayto "$n" So, you refuse?  Help me find someone else then.  Ok?
		mpjunk $o
	endif
~
</DATA></FILE>
<FILE><NAME>$QUEST_ID_deliveree.script</NAME>
<DATA>

PUT_PROG all
	if EVAL($p == '$[1 *]') AND EVAL($o == '$[2 *]') AND EVAL('$[2 *]' != '')
		if ISLIKE($n '$QUEST_CRITERIA') AND EVAL('$[1 *]' != '') and ISPC($n)
			if  EVAL('$MULTIPLE' != 'YES') AND QUESTWINNER($n *)
				# do nothing
			else
				if EVAL($p == '$[1 *]')
					if EVAL('$EXP' != '')
						mpexp $n $EXP
					endif
					if EVAL('$FACTION' != '')
						mpfaction $n $FACTION $NUMFACTION
					endif
					if EVAL('$QUEST_POINT' == 'YES')
						mpoload QuestPoint
						mpforce $n GET "a quest point"
					endif
					if EVAL('$GOLD' != '') AND EVAL('$GOLD' > 0)
						mpoload $GOLD
						mpforce $n GET $GOLD
					endif
					mpquestwin $n *
					mpecho $DELIVER_RESPONSE
					mpendquest *
					IF EVAL('$NEXT_QUEST' != '')
						mpstartquest $NEXT_QUEST
					ENDIF
				endif
			endif
		endif
	endif
~

DROP_PROG all
	if EVAL('$[2 *]' != '') 
		RETURN
	ENDIF
	if ISLIKE($n '$QUEST_CRITERIA') AND EVAL('$[1 *]' != '') and ISPC($n)
		if  EVAL('$MULTIPLE' != 'YES') AND QUESTWINNER($n *)
			# do nothing
		else
			if EVAL($o == '$[1 *]')
				if EVAL('$EXP' != '')
					mpexp $n 10%
				endif
				if EVAL('$FACTION' != '')
					mpfaction $n $FACTION $NUMFACTION
				endif
				if EVAL('$QUEST_POINT' == 'YES')
					mpoload QuestPoint
					mpforce $n GET "a quest point"
				endif
				if EVAL('$GOLD' != '') AND EVAL('$GOLD' > 0)
					mpoload $GOLD
					mpforce $n GET $GOLD
				endif
				mpquestwin $n *
				mpecho $DELIVER_RESPONSE
				mpendquest *
				IF EVAL('$NEXT_QUEST' != '')
					mpstartquest $NEXT_QUEST
				ENDIF
			endif
		endif
	endif
~
</DATA></FILE>
