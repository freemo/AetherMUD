#!QUESTMAKER_START_SCRIPT Delivery #1
#This quest will allow you to designate an existing mob on your map
#as needing an item, which you will create, to be delivered to another
#existing mob on your map.  When the player does this, an optional reward is given.
#
#!QUESTMAKER_PAGE Quest Name/Duration
#Enter the unique name and starting time/duration for your new quest.
#
#Quest Name:
#Enter a unique name for your delivery quest:
#$QUEST_NAME=$UNIQUE_QUEST_NAME=delivery_1
#
#Quest Frequency:
#A quest starts and stops on timed intervals.  Enter below the amount of time 
#that this quest will remain unavailable between periods of availability.
#This time is expressed in "ticks" (4 second periods).  You may enter math 
#expressions, and use the special ? operator to generate random numbers.
#For instance, a frequency of 5 + 3?10 would generate a frequency of 8-15 
#ticks.
#$FREQUENCY=$EXPRESSION=400
#
#Quest Duration:
#A quest starts and stops on timed intervals.  Enter below the amount of time 
#that this quest will run and remain available after it is started.
#This time is also the amount of time a player has to delivery the item
#after they have retreived it from the deliverer mob.  
#This time is expressed in "ticks" (4 second periods).  You may enter math 
#expressions, and use the special ? operator to generate random numbers.
#For instance, a duration of 5 + 3?10 would generate a duration of 8-15 
#ticks.
#$DURATION=$EXPRESSION=175
#
#!QUESTMAKER_PAGE Specify the Deliverer
#The Deliverer is the mob who will be holding the item initially. This must
#be an existing mob in your world.  
#
#Deliverer Area/Room
#Specify below the name of the area that your deliverer mob will be located
#in and/or the room in which the mob can be found.  You may leave both blank
#to force the quest manager to find the mob somewhere in the world, but this
#will hurt the performance of your mud, so use with care.
#  
#Deliverer Area:
#$DELIVERER_AREA=($AREA)
#Deliverer Room ID:
#$DELIVERER_ROOM=($ROOMID)
#
#Deliverer mob name
#Specify the name of the mob who will act as the deliverer mob.  
#Use double-quotes around multi-word names.
#If you specified an area or room above, then the mob must be located there.
#You may optionally have the quest manager select a 'random' mob by entering
#a mob name of ANY MASK= followed by a Zapper Mask (see help on 
#Prop_HaveZapper for information on zapper mask syntax).
#Remember, if the quest manager can not find your mob, the quest will not
#be started.
#$DELIVERER_NAME_WORDS=$STRING
#
#Deliverer reasons
#You may optionally specify some additional bit of speech the deliverer 
#mob will say to players when the item is handed over for delivery.  The
#deliverer will already be specifying the name of the mob to deliver to,
#but you might want to say what its for, or where the mob is, or something
#about the reward, or whatever.
#$REASONING=($STRING)
#
#!QUESTMAKER_PAGE Specify the Deliveree
#The Deliveree is the mob to which the item will be delivered. 
#
#Deliveree Area/Room
#Specify below the name of the area that your deliveree mob will be located
#in and/or the room in which the mob can be found.  You may leave both blank
#to force the quest manager to find the mob somewhere in the world, but this
#will hurt the performance of your mud, so use with care.  
#Deliveree Area:
#$DELIVEREE_AREA=($AREA)
#Deliveree Room:
#$DELIVEREE_ROOM=($ROOMID)
#
#Deliveree mob name
#Specify the name of the mob who will act as the deliveree mob.  
#Use double-quotes around multi-word names.
#If you specified an area or room above, then the mob must be located there. 
#You may optionally have the quest manager select a 'random' mob by entering
#a mob name of ANY MASK= followed by a Zapper Mask (see help on 
#Prop_HaveZapper for information on zapper mask syntax).
#Remember, if the quest manager can not find your mob, the quest will not
#be started.
#$DELIVEREE_NAME_WORDS=$STRING
#
#!QUESTMAKER_PAGE Deliverable item
#Create the item which the deliverer mob will give to the player to 
#be taken and given to the deliveree mob.  You may select from the
#available items below or create a new item just for this quest.
#
#$DELIVERABLE_ITEM=$ITEMXML
#
#!QUESTMAKER_PAGE Quest Completion
#Select some rewards for delivering the item:
#Quest Point?
#$QUEST_POINT=$CHOOSE=YES,NO
#Amount of money (an amount, or leave blank)
#$GOLD=($EXPRESSION)
#Amount of experience points, blank for none, or a number% for percent of exp to next level.
#$EXP=($STRING)=10%
#
#Select whether a player may complete this quest multiple times:
#$MULTIPLE=$CHOOSE=YES,NO
#!QUESTMAKER_END_SCRIPT Delivery #1

set name $QUEST_NAME

set wait $FREQUENCY
set interval 1
#quiet

import items item.xml
set area $DELIVERER_AREA
set room $DELIVERER_ROOM
set mobgroup
set mob $DELIVERER_NAME_WORDS
give behavior Scriptable LOAD=deliverer.script
load item any
give item

set area $DELIVEREE_AREA
set room $DELIVEREE_ROOM
set mobgroup
set mob $DELIVEREE_NAME_WORDS
give behavior Scriptable LOAD=deliveree.script

set duration $DURATION

#!~!BEGINFILEXML!~!
<FILE><NAME>item.xml</NAME><DATA><ITEMS>$DELIVERABLE_ITEM</ITEMS></DATA></FILE>
<FILE><NAME>deliverer.script</NAME>
<DATA>
# this script is for the human chosen to be the note bearer
GREET_PROG 100
	if EVAL('$MULTIPLE' == 'YES') OR !QUESTWINNER($n *)
		if has($n '$[1 *]')
				sayto "$n" What are you doing back here?  Deliver $[1 *] to ${2 *}!
		else
			if has($i '$[1 *]')
				sayto "$n" I have something I need to be delivered to ${2 *}.  If you'll deliver it for me, say 'I accept', and I will give you the details.
			endif
		endif
	endif
~

SPEECH_PROG mission problem package quest help delivery deliver 'me to do' 'to do' mission? problem? package? quest? help? delivery? delive?r 'me to do?' 'to do?' 
	if EVAL('$MULTIPLE' == 'YES') OR !QUESTWINNER($n *)
		if has($n '$[1 *]')
				sayto "$n" Just deliver $[1 *] to ${2 *}!
		else
			if has($i '$[1 *]')
				sayto "$n" I have something I need to be delivered to ${2 *}.  If you'll deliver it for me, say 'I accept', and I will give you the details.
			endif
		endif
	endif
~

SPEECH_PROG p i accept
	if  EVAL('$MULTIPLE' == 'YES') OR !QUESTWINNER($n *)
		if has($i '$[1 *]') and !has($n '$[1 *]')
#			mpoload '$[1 *]'
			give "$[1 *]" "$n"
			sayto "$n" Please deliver this to ${2 *}.  $REASONING
			mpqset * REMAINING $DURATION
		else
			if has($n '$[1 *]')
				sayto "$n" Yes, I know.  Now deliver it to ${2 *}.
			endif
		endif
	endif
~
	

GIVE_PROG all
	if EVAL($o == '$[1 *]')
		sayto "$n" So, you refuse?  Help me find someone else then.  Ok?
		mpjunk $o
	endif
~
</DATA></FILE>
<FILE><NAME>deliveree.script</NAME>
<DATA>
GREET_PROG 100
	if has($n '$[1 *]')
		say "$n" Ah, I see you are here to deliver $[1 *].  Go ahead and give it to me.
	endif
~
	

GIVE_PROG all
	if  EVAL('$MULTIPLE' != 'YES') AND QUESTWINNER($n *)
		sayto "$n" What's this?
		give "$o" "$n"
	else
		if EVAL($o == '$[1 *]')
			sayto "$n" Thank you.
			if EVAL('$EXP' != '')
				mpexp $n 10%
			endif
			if EVAL('$QUEST_POINT' == 'YES')
				mpoload QuestPoint
				give "a quest point" "$n"
			endif
			if EVAL('$GOLD' != '') AND EVAL('$GOLD' > 0)
				mpoload $GOLD
				give $GOLD "$n"
			endif
			mpquestwin $n *
			mpjunk $o
			mpendquest *
		else
			sayto "$n" What's this?
			give "$o" "$n"
		endif
	endif
~
</DATA></FILE>
